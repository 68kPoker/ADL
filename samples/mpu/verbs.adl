VERB
  pry,  strike, wake,   say,
  beam, join,   opener, glow,
  grab, weigh,  rname,  score,
  debug;

feel	= touch;
shake	= wake;
hit	= strike;
kill	= strike;
shut	= close;
push	= move;
pull	= move;
lift	= move;
destroy	= break;
incant	= say;
chant	= say;
shout	= say;
yell	= say;
out	= exit;


cg =
    ($say "You can't go that way.\n")
    ($setg MyLoc ($loc .ME))
;

die =
    ($say "You are dead.  Better luck next time.\n")
    (IF ($not @Wizrd) THEN
 	($spec 3)
    )
;



Looks =
    (IF ($dobj) THEN
	($say "Huh?\n")
	($exit 1)
    )
    ($setg GOVERB TRUE)
    ($setg MyLoc -1)
;

n (PREACT) = Looks;
s (PREACT) = Looks;
e (PREACT) = Looks;
w (PREACT) = Looks;

nw(PREACT) = Looks;
ne(PREACT) = Looks;
sw(PREACT) = Looks;
se(PREACT) = Looks;

u (PREACT) = Looks;
d (PREACT) = Looks;

out (PREACT) = Looks;
enter (PREACT) = Looks;

say (PREACT) =
    (Expect ($or NO_OBJ ONE_OBJ STR_OBJ PLAIN_OBJ) NO_OBJ)
    (IF ($not ($dobj)) THEN
	($say "Type just what you want to say: ")
	($setg Dobj ($read))
    )
    (IF ($ge ($dobj) 0) THEN
	($say "You want to make " ($name ($dobj)))
	($say " a linguistic artifact??  Strange!\n")
	($exit 1)
    )
;
say (ACTION) = 
    (IF ($eqst ($dobj) Shazm) THEN
	(WzTgl)
     ELSE
	($say "Very well.  '")
	($say ($dobj))
	($say "'.\n")
    )
;


DWIMD =
    ($return (Dwimmer %1))
;

DWIMI =
    ($return (Dwimmer %1))
;


wake(PREACT) = Preact;
wake(ACTION) =
    ($say "I don't know how to wake " ($name ($dobj)) ".\n")
;


strike(PREACT) = Preact;
strike(ACTION) =
    ($say "Hitting the " ($name ($dobj)) " doesn't seem to do anything.")
;


pry(PREACT) =
    (Expect ($or ONE_OBJ PLAIN_OBJ) ($or ONE_OBJ PLAIN_OBJ))
    (CheckAvail)
;
pry(ACTION) =  ($say "I can't seem to manage that.\n");


Tress =
    ($say "You were warned about trespassing! The fence is electrified!\n")
    (die)
;


score(ACTION) =
    (IF ($eq ($loc .ME) town5) THEN
 	($say
"A mysterious glowing network of lights appears, and you can
just make out the words:
   Your account is now at $" ($str @SCORE) ".
   Thank you for letting Arpa-Citizen's be your host.
The network then disappears.\n"
	)
     ELSE
	($say "Hmm, you'll have to check at the local bank.\n")
    )
;


WizP =
    (IF ($not @Wizrd) THEN
	($say "Only a real Wizard can do that!\n")
	($exit 1)
    )
;

rname(PREACT) =
    (IF @Dobj THEN
	(WizP)
	(($sdesc @Dobj))
     ELSE
	(($sdesc ($loc .ME)))
    )
    ($exit 1)
;
beam(PREACT) = WizP;
grab(PREACT) = WizP;
join(PREACT) = WizP;
opener(PREACT) = WizP;
glow(PREACT) = WizP;
weigh(PREACT) = WizP;
debug(PREACT) = WizP;

beam(ACTION) =
    (IF ($dobj) THEN
	($move .ME ($dobj))
	($setg MyLoc -1)
    )
;

join(ACTION) =
    (IF ($dobj) THEN
	($move .ME ($loc ($dobj)))
	($setg MyLoc -1)
    )
;

opener(ACTION) =
    (IF ($dobj) THEN
	($setp ($dobj) OPENED TRUE)
	($say "Opened.\n")
    )
;

glow(ACTION) =
    ($setp .ME LIGHT ($not ($prop .ME LIGHT)))
    (IF ($prop .ME LIGHT) THEN
	($say "Your body begins to radiate a strong but gentle white light!\n")
     ELSE
	($say "The light from your body fades away.\n")
    )
;

grab(ACTION) =
    (IF ($dobj) THEN
	($say "You have the " ($name ($dobj)) ", Boss!\n")
	($move ($dobj) .ME)
	($setp .ME HAS ($plus ($prop .ME HAS) ($prop ($dobj) WEIGH)))
    )
;

weigh(ACTION) =
    (IF @Dobj THEN
	($say "The " ($name @Dobj) " weighs " ($str ($prop @Dobj WEIGH)))
     ELSE
	($say "You are carrying " ($str ($prop .ME HAS)))
    )
    ($say " pounds, Boss!\n")
;

debug(ACTION) =
    ($spec 1)	{ Turn on debugging }
;
