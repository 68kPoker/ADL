LOOK =
    (IF ($and @GOVERB ($not @GO)) THEN
	(cg)
    )
    ($setg GOVERB FALSE)
    ($setg GO FALSE)
    (IF @IncFlag THEN
	($incturn)
    )
    ($setg IncFlag TRUE)
;


PROMPT =
    ($spec 9 (($sdesc ($loc .ME)) 1) @SCORE ($turns))
    ($say "> ")
;


GrowX =
    (IF ($prop %1 SHRNK) THEN
	($setp %1 SHRNK FALSE)
	(IF ($cont %1) THEN
	    (GrowX ($cont %1))
	)
	(IF ($link %1) THEN
	    (GrowX ($link %1))
	)
    )
;


Grow =
    (IF ($prop .ME SHRNK) THEN
	($setp .ME SHRNK FALSE)
	(IF ($cont .ME) THEN
	    (GrowX ($cont .ME))
	)
    )
;


ShrnX =
    (IF ($not ($prop %1 SHRNK)) THEN
	($setp %1 SHRNK TRUE)
	(IF ($cont %1) THEN
	    (ShrnX ($cont %1))
	)
	(IF ($link %1) THEN
	    (ShrnX ($link %1))
	)
     ELSE
	($say "You hear a tiny POP as the " ($name %1)" vanishes completely!\n")
	(IF ($link %1) THEN
	    (ShrnX ($link %1))
	)
	($move %1 .ALL)
    )
;


Shrink =
    (IF ($not ($prop .ME SHRNK)) THEN
	($setp .ME SHRNK TRUE)
	(IF ($cont .ME) THEN
	    (ShrnX ($cont .ME))
	)
    )
;

WzTgl =			{ Toggle the Wizard flag }
    ($setg Wizrd ($not @Wizrd))
    (IF @Wizrd THEN
	($say
"You hear a low rumble of thunder, shaking the very ground on
which you stand.  Suddenly, there is a blazing flash of light!!
You are unharmed, but feal great power flowing in your body.\n"
	)
     ELSE
	($say "Your wizardly powers unceremoniously fade away.\n")
    )
;


TakeAct =
    (IF ($eq ($prop @Dobj WEIGH) CAPAC) THEN
	($say "You can't move ")
	(($sdesc @Dobj))
	($say ".\n")
	($exit 1)
     ELSEIF ($and ($prop .ME SHRNK) ($not ($prop @Dobj SHRNK))) THEN
	($say "Right now, the " ($name @Dobj)
	      " is too big for you to deal with.\n")
	($exit 1)
     ELSEIF ($gt ($plus ($prop .ME HAS) ($prop @Dobj WEIGH))
		 ($prop .ME HOLDS)
	    )
     THEN
	($say "You can't carry that much more!\n")
	($exit 1)
     ELSE
	($setp .ME HAS ($plus ($prop .ME HAS) ($prop @Dobj WEIGH)))
    )
;


DropAct =
    ($setp .ME HAS ($minus ($prop .ME HAS) ($prop @Dobj WEIGH)))
    (IF ($eq ($loc .ME) cel13) THEN
	(IF ($not ($prop cel13 HOLED)) THEN
	    (IF ($gt ($prop @Dobj WEIGH) 75) THEN
		($say "The " ($name @Dobj)
		      " breaks through the ice and sinks!\n")
		($setp cel13 HOLED TRUE)
		($move hole cel13)
		($move @Dobj .ALL)
		($setg Skip TRUE)
	     ELSE
		($say "The ice chips a bit, but does not break.\n")
	    )
	)
     ELSEIF ($eq ($loc .ME) cel19) THEN
	(IF ($not ($prop goblet FREED)) THEN
	    (IF ($gt ($prop @Dobj WEIGH) 75) THEN
	    	($say "The " ($name @Dobj))
	    	($say " cracks the ice, and the goblet is freed!\n")
	   	($setp goblet FREED TRUE)
	     ELSE
		($say "The ice chips a bit, but does not break.\n")
	    )
	)
    )
;
