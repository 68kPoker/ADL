{*** ROUTINE DECLARATIONS ***}

ROUTINE
  darkq, ei8, scads, ctake, cdrop, scord, exitg, onlmp;


{*** DEAD-END ROUTINES ***}

PROMPT =
    ($spec 9 (($sdesc ($loc .ME)) 1) ($prop .ME pscore) ($turns))
    ($say "> ")
;

cg = ($say "Can't go that way.\n");

tosml = ($say "The hole is too small for you to fit through.\n");

dumdir = ($say "I don't know which direction that is.\n");

tohigh = ($say "The hole is too high for you to reach.\n");

nothe = ($say "This section is not implemented yet.\n");

skore =
    ($say
	"You scored "
	($str ($prop .ME pscore))
	" out of "
	($str ($prop .ME pmax))
	" possible points.\n"
    )
;


Rating0 = "junior beginning";
Rating1 = "senior beginning";
Rating2 = "intermediate";
Rating3 = "expert";
Rating4 = "junior master";
Rating5 = "master";
Rating6 = "senior master";
Rating7 = "life master";
Rating8 = "super-stud";


ratng =
    ($setp .ME ratvl 0)
    (IF ($prop .ME pscore) THEN
	($setp .ME ratvl ($div	($times ($prop .ME pscore) 8)
					($prop .ME pmax)))
    )
    ($say
	"That gives you a ranking of "
	($plus Rating0 ($prop .ME ratvl))
	" adventurer.\n"
    )
;


ratnx =
    (IF ($eq ($prop .ME ratvl) 8) THEN
	($say "CONGRATULATIONS.\n")
	($spec QUIT)
    )
    ($setp .ME ratvl ($plus ($prop .ME ratvl) 1))
    ($setp .ME ratvl ($times ($prop .ME ratvl) ($prop .ME pmax)))
    ($setp .ME ratvl ($div ($prop .ME ratvl) 8))
    ($setp .ME ratvl ($plus ($prop .ME ratvl) 1))
    ($setp .ME ratvl ($minus ($prop .ME ratvl) ($prop .ME pscore)))
    ($say
	"To achieve the next higher rating, you need to score "
	($str ($prop .ME ratvl))
	" more points.\n"
    )
;


endgame =
    ($incturn)
    (IF ($eq ($prop .ME pscore) ($prop .ME pmax)) THEN
	($say
"     As you drop the last treasure, the lights come up and you hear a voice
say, \"CONGRATULATIONS, ADVENTURER.  You have restored the museum
displays to their former glory.  Now accept the Director's thanks, and
also his reward!\"\n"
"     You are miraculously transported to a room you have never seen before,
filled to the top with precious gold and jewels.  The voice appears again,
saying, \"Your reward is as follows:  you are granted the privilege of\n"
"becoming the new curator of the Museum.  Your first responsibility is
to catalog and sort these treasures for placement in the Museum.  Your
salary is $3.25 per hour.  Again, thanks!\"\n"
"     The voice disappears, and you are left alone in the immense room.
You'd better get to work, for you have a LOT to do!\n"
	)
	($spec QUIT)
    )
;
