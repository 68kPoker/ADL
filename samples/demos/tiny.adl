INCLUDE "standard.adl";

NOUN startrm, brightroom;		{ Locations in the dungeon }
startrm(LIGHT) = TRUE;		brightroom(LIGHT) = TRUE;

cg = ($say "You can't go that way.\n");

startrm(LDESC) =
    ($say "You are in a small but comfortable room.  You hardly want ")
    ($say "to leave, but there is a door leading east, if you insist.\n")
;
startrm (SDESC) = ($say "Comfortable room.\n");
startrm(ACTION) =
    ($miss    cg cg 0          cg 0 0 0 0 0 0)
    ($hit .ME 0  0  brightroom 0  0 0 0 0 0 0)
;

brightroom(LDESC) =
    ($say "You are in a brightly lit room.  The walls sparkle with ")
    ($say "scintillating lights.  There is a darker room to the west.\n")
;
brightroom(SDESC) = ($say "Bright room.\n");
brightroom(ACTION) =
    ($miss    cg cg cg 0       0 0 0 0 0 0)
    ($hit .ME 0  0  0  startrm 0 0 0 0 0 0)
;

ADJEC red, blue;

NOUN red pillow(startrm), blue pillow(startrm);

red pillow(LDESC) = ($say "There is a red pillow here.\n");
red pillow(SDESC) = ($say "A red pillow");

blue pillow(LDESC) = ($say "There is a blue pillow here.\n");
blue pillow(SDESC) = ($say "A blue pillow");

NOUN platinum(brightroom);		bar = platinum;
platinum(LDESC) = ($say "There is a bar of platinum here!\n");
platinum(SDESC) = ($say "A platinum bar");
platinum(ACTION) = 
    (IF ($and ($eq ($verb) drop) ($eq ($loc .ME) ($loc [red pillow]))) THEN
	($say "The bar falls onto the red pillow, breaking it!  The symbolism ")
	($say "impresses itself upon you, and you go back to work instead of ")
	($say "playing these silly games!\n")
	($spec 3)
    )
;

NOUN SELF(startrm);		SELF(NOTAKE) = TRUE;

START =
    (StdInit SELF)
;

DWIMD = ($return (Dwimmer %1));
DWIMI = ($return (Dwimmer %1));
